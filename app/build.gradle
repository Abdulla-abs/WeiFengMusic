apply plugin: 'com.android.application'
apply plugin: 'dagger.hilt.android.plugin'

// 1. 定义生成任务
task generateDependencyList {
    // 输出文件路径（会自动生成在源码里，编译时能直接引用）
    def outputFile = file("${project.buildDir}/generated/source/dependencies/com/wei/music/BuildDependencies.java")

    outputs.file(outputFile)                 // 让 Gradle 知道这个文件是输出
    outputs.upToDateWhen { false }           // 每次构建都重新生成（也可以去掉，增量构建）

    doLast {
        def deps = [] as SortedSet // 自动去重 + 排序

        // 遍历所有可能包含依赖的 configuration（包括多 flavor、多模块）
        project.configurations.each { config ->
            if (config.canBeResolved) {
                try {
                    config.resolvedConfiguration.resolvedArtifacts.each { artifact ->
                        def id = artifact.moduleVersion.id
                        deps << "${id.group}:${id.name}:${id.version}"
                    }
                } catch (Exception ignored) {
                    // 某些 configuration 可能无法 resolved，忽略即可
                }
            }
        }

        // 生成 Java 类内容
        def javaContent = """// =============================================
// 此文件由 Gradle 自动生成，请勿手动修改！
// 生成时间: ${new Date()}
package com.wei.music;

import java.util.Arrays;
import java.util.List;

public class BuildDependencies {
    public static final long BUILD_TIME = ${System.currentTimeMillis()}L;

    public static final List<String> LIBRARIES = Arrays.asList(
${deps.collect { "        \"${it}\"" }.join(',\n')}
    );
}
"""

        outputFile.parentFile.mkdirs()
        outputFile.write(javaContent, "UTF-8")
        println "已生成 ${deps.size()} 个依赖到 BuildDependencies.java"
    }
}

// 2. 让编译任务依赖这个生成任务
tasks.whenTaskAdded { task ->
    if (task.name.contains("compile") && (task.name.contains("Java") || task.name.contains("Kotlin"))) {
        task.dependsOn generateDependencyList
    }
}

android {
    namespace 'com.wei.music'
    compileSdkVersion 34

    defaultConfig {
        applicationId "com.wei.music"
        minSdkVersion 24
        targetSdkVersion 34
        versionCode 1
        versionName "1.0"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    sourceSets {
        main {
            java.srcDirs += "${project.buildDir}/generated/source/dependencies"
        }
    }

    lintOptions {
        abortOnError false
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        viewBinding = true
    }

    viewBinding {
        enable = true
    }
}

dependencies {
    implementation(project(":myutil"))
    implementation fileTree(dir: "libs", include: ["*.jar"])

    // https://mvnrepository.com/artifact/com.blankj/utilcodex
    implementation 'com.blankj:utilcodex:1.31.1'

    implementation 'jakarta.inject:jakarta.inject-api:2.0.1'
    implementation 'com.google.dagger:hilt-android:2.51'
    annotationProcessor 'com.google.dagger:hilt-compiler:2.51'

    implementation "androidx.lifecycle:lifecycle-livedata:2.8.6"
    implementation "androidx.lifecycle:lifecycle-viewmodel:2.8.6"

    implementation "androidx.room:room-runtime:2.6.1"
    implementation("androidx.room:room-rxjava3:2.6.1")
    annotationProcessor "androidx.room:room-compiler:2.6.1"

    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.3'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    //rxjava
    implementation "com.github.akarnokd:rxjava3-retrofit-adapter:3.0.0"
    implementation 'io.reactivex.rxjava3:rxandroid:3.0.2'
    implementation 'io.reactivex.rxjava3:rxjava:3.1.5'
    implementation 'com.uber.autodispose2:autodispose:2.1.1'
    implementation 'com.uber.autodispose2:autodispose-android:2.1.1'
    implementation 'com.uber.autodispose2:autodispose-lifecycle:2.1.1'
    implementation 'com.uber.autodispose2:autodispose-androidx-lifecycle:2.1.1'

    implementation 'com.blankj:utilcodex:1.31.1'

    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'com.google.android.material:material:1.10.0'
    implementation "androidx.viewpager2:viewpager2:1.0.0"
    implementation "androidx.media:media:1.7.1"
    //implementation "androidx.media3:media3-session:1.3.1"  // 如果用 Media3，优先它

    implementation 'com.github.bumptech.glide:glide:4.16.0'
    implementation 'jp.wasabeef:glide-transformations:4.3.0'

    implementation 'androidx.palette:palette:1.0.0'

    implementation 'com.tencent:mmkv-static:1.2.10'

    implementation 'com.squareup.okhttp3:okhttp:3.8.0'
    implementation 'com.squareup.okio:okio:1.12.0'

    implementation 'com.google.code.gson:gson:2.8.2'

    implementation 'com.baidu.mobstat:mtj-sdk:4.0.0.7'

    implementation 'ren.qinc.edgetranslucent:lib:0.0.3'

    implementation 'com.drakeet.drawer:drawer:1.0.3'
}
